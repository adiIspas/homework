capacitateMaxima EQU 10000

; se predefineste constanta “capacitateMaxima”

alimentareBancomat1 VARIABLE capacitateMaxima - P$sumaCurenta1
alimentareBancomat2 VARIABLE capacitateMaxima - P$sumaCurenta2

; se predefineste o expresie care variaza ın sistem pentru modelarea sumei cu care
; angajatul unei banci alimenteaza bancomatul daca este necesar

sumaExtrasa VARIABLE 10#(RN1@196) + 50
durataTranzactiei FUNCTION RN1,D5 
0.25,1/0.65,3/0.85,4/0.95,5/1,6
bancomat1 STORAGE 10000
bancomat2 STORAGE 10000

; bancomat1 (bancomat2) – numele entitatii de depozitare, va contine suma existenta
; ın bancomat; dupa depunerea initiala va fi de 10000 de lei ın fiecare dintre
; bancomate

; capacitateMaxima – numarul maxim de unitati de depozitare, suma maxima
; care poate exista ın bancomat
; modelarea alimentarii initiale a bancomatului
; initial entitatea de depozitare contine 0 unitatde depozitare ocupate

GENERATE ,,,1,2
ENTER bancomat1,capacitateMaxima
ENTER bancomat2,capacitateMaxima
TERMINATE

; modelarea extragerilor din bancomat
GENERATE (Exponential(1,0,20))
; A – repartitia exponentiala care modeleaza timpul de intersosire a clientilor
TRANSFER 0.5,,mergiLaBancomat2
SEIZE banc1
ASSIGN 1,banc1
ASSIGN suma1,V$sumaExtrasa
TEST GE S$bancomat1,P$suma1,pleaca
LEAVE bancomat1,P$suma1
TRANSFER ,durata
mergiLaBancomat2 SEIZE banc2
ASSIGN 1,banc2
ASSIGN suma2,V$sumaExtrasa
TEST GE S$bancomat2,P$suma2,pleaca
LEAVE bancomat2,P$suma2
durata ADVANCE FN$durataTranzactiei

; durataTranzactiei – poate lua una dintre valorile i = 3, 5, 8, 10, 12 cu probabilitatile
; asociate pi = 0.25, 0.4, 0.2, 0.1, 0.05

pleaca RELEASE P1
TERMINATE 1

; modelarea alimentarii bancomatului la fiecare 2 ore daca angajatul constata ca
; bancomatul detine o suma mai mica de 5000 de lei

GENERATE 120

; A2 – intervalul de timp dupa care angajatul verifica daca bancomatul trebuie
; alimentat

ASSIGN sumaCurenta1,S$bancomat1
TEST LE P$sumaCurenta1,5000,nuAlimenteaza

ENTER bancomat1,V$alimentareBancomat1
ASSIGN sumaCurenta2,S$bancomat2
TEST LE P$sumaCurenta2,5000,nuAlimenteaza
ENTER bancomat2,V$alimentareBancomat2
nuAlimenteaza TERMINATE

GENERATE 1
TERMINATE 1
